// Description: Find all devices that are internet-facing by keying off of the defender event for when devices get detected as internet facing
// Author: max-h471
DeviceInfo
| where TimeGenerated > ago(30d)
| where IsInternetFacing
// Exclude your organizations expected egress NAT IP's which are expected
//| where PublicIP != "192.168.0.0"
// Describes to the analyst why this device is internet facing and how it was identified
| extend InternetFacingInfo = AdditionalFields
| extend InternetFacingReason = extractjson("$.InternetFacingReason", InternetFacingInfo, typeof(string)), InternetFacingLocalPort = extractjson("$.InternetFacingLocalPort", InternetFacingInfo, typeof(int)), InternetFacingScannedPublicPort = extractjson("$.InternetFacingScannedPublicPort", InternetFacingInfo, typeof(int)), InternetFacingScannedPublicIp = extractjson("$.InternetFacingScannedPublicIp", InternetFacingInfo, typeof(string)), InternetFacingLocalIp = extractjson("$.InternetFacingLocalIp", InternetFacingInfo, typeof(string)), InternetFacingTransportProtocol=extractjson("$.InternetFacingTransportProtocol", InternetFacingInfo, typeof(string)), InternetFacingLastSeen = extractjson("$.InternetFacingLastSeen", InternetFacingInfo, typeof(datetime)) 
| summarize arg_max(TimeGenerated, *) by DeviceId
