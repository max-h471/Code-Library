rule Process_Dump_Created {

  meta:
    author = "max-h471"
    description = "Detects uses of various LOLBin process dump utilities"
    reference = "https://redcanary.com/threat-detection-report/techniques/lsass-memory/"
    rule_name = "Process Dump Created"
    tactic = "TA0006"
    technique = "T1003.001"
    log_type = "MICROSOFT_DEFENDER_ENDPOINT"    
    severity = "Low"

  events:
    //$process.metadata.event_type = "PROCESS_LAUNCH"
    // detection focuses on all proccess dump use, but additional scrutiny on lsass process
    (
    re.regex($process.target.process.file.full_path, `\\createdump\.exe$`) nocase or
    $process.src.process.file.full_path = "createdump.exe" nocase or
    $process.target.process.command_line = /procdump(\.exe)? .* lsass\.exe/ nocase or
    $process.target.process.command_line = /rundll32(\.exe)?.*comsvcs\.dll.*MiniDump/ nocase or 
    $process.principal.process.command_line = /rundll32(\.exe)?.*comsvcs\.dll.*MiniDump/ nocase or
    $process.target.process.command_line = /procdump(\.exe)?.*lsass\.exe/ nocase
    )
    /* these strings can be added if additional arguments are needed for tuning
    (
        strings.contains(strings.to_lower($process.target.process.command_line), " -u ") or
        strings.contains(strings.to_lower($process.target.process.command_line), " --full ") or
        strings.contains(strings.to_lower($process.target.process.command_line), " -f ") or
        strings.contains(strings.to_lower($process.target.process.command_line), " --name ") or
        strings.contains(strings.to_lower($process.target.process.command_line), ".dmp ") or
        strings.contains(strings.to_lower($process.target.process.command_line), "-ma ") or
        strings.contains(strings.to_lower($process.target.process.command_line), "MiniDump ")
    )
    */
    
    $process.principal.hostname = $hostname

  match:
    $hostname over 5m

  outcome:
    $principal_process_pid = array_distinct($process.principal.process.pid)
    $principal_process_command_line = array_distinct($process.principal.process.command_line)
    $principal_process_file_sha256 = array_distinct($process.principal.process.file.sha256)
    $principal_process_file_full_path = array_distinct($process.principal.process.file.full_path)
    $principal_process_product_specfic_process_id = array_distinct($process.principal.process.product_specific_process_id)
    $principal_process_parent_process_product_specfic_process_id = array_distinct($process.principal.process.parent_process.product_specific_process_id)
    $target_process_pid = array_distinct($process.target.process.pid)
    $target_process_command_line = array_distinct($process.target.process.command_line)
    $target_process_file_sha256 = array_distinct($process.target.process.file.sha256)
    $target_process_file_full_path = array_distinct($process.target.process.file.full_path)
    $target_process_product_specfic_process_id = array_distinct($process.target.process.product_specific_process_id)
    $principal_user_userid = array_distinct($process.principal.user.userid)
    $log_type = array_distinct(strings.concat($process.metadata.log_type,"/",$process.metadata.product_event_type))

  condition:
    $process
}
